# View each selected Sound (and TextGrid) object in turn
#
# The script allows for easy navigation between selected Sound
# objects, which is particularly useful when comparing specific
# features in each of them. If an equal number of TextGrid and
# Sound objects have been selected, they will be paired by name
# and viewed in unison.
#
# Written by Jose J. Atria (October 14, 2012)
# Last revision: July 10, 2014)
#
# This script is free software: you can redistribute it and/or
# modify it under the terms of the GNU General Public License as
# published by the Free Software Foundation, either version 3 of
# the License, or (at your option) any later version.
#
# A copy of the GNU General Public License is available at
# <http://www.gnu.org/licenses/>.

procedure foreach.action ()
  if numberOfSelected()
    @has_editor()
    if has_editor.editor
      do(has_editor.command$)
      editor: has_editor.editor
        .editor$ = Editor info
      endeditor

      .name$ = extractLine$(.editor$, "Editor name: ")
      .name$ = mid$(.name$, rindex(.name$, " "), length(.name$))

      beginPause: "Viewing " + .name$ + " " +  "(" +
        ... string$(foreach.item) + " of " +
        ... string$(foreach.total_items) + ")"

      if foreach.item > 1
        .button = endPause: "Stop", "Previous", if foreach.item = foreach.total_items then
          ... "Finish" else "Next" fi, 3, 1
      else
        .button = endPause: "Stop", if foreach.item = foreach.total_items then
          ... "Finish" else "Next" fi, 2, 1
      endif

      if .button = 1
        # Pressed stop
        foreach.next = 0
      elsif .button = 2 and foreach.item > 1
        # Pressed back
        foreach.next = -1
      else
        # Pressed forward
        foreach.next = if foreach.item = foreach.total_items then 0 else 1 fi
      endif

      nocheck editor: has_editor.editor
        nocheck Close
      nocheck endeditor
    endif
  endif
endproc

include ../procedures/foreach.proc
procedure vieweach ()
  @foreach()
endproc

procedure has_editor ()
  .command$ = "View & Edit"
  @_try_editor: .command$
  if '_try_editor.return'
    .command$ = "Edit"
    @_try_editor: .command$
  endif
  .editor = '_try_editor.return'
endproc

procedure _try_editor: .command$
  nocheck do(.command$)
  for .i to numberOfSelected()
    nocheck editor: selected(.i)
      .editor$ = nocheck Editor info
      nocheck Close
    nocheck endeditor
    .return = if .editor$ = "" then 0 else selected(.i) fi
  endfor
endproc
